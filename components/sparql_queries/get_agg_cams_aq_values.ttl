# Give me the daily average of the CO values measured for Linz in January 2020

PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#> 
PREFIX gadm: <http://example.com/ontologies/gadm#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?obs_time (AVG(?obs_result) AS ?daily_avg)
WHERE {
    {
        SELECT ?foi_ent ?cell_geom ?gadm_name
        WHERE {
            ?foi_ent a sosa:FeatureOfInterest ;
                geo:intersects ?gadm_ent ;
                geo:hasGeometry ?cell_geom_ent .
            ?cell_geom_ent geo:asWKT ?cell_geom .
            ?gadm_ent a gadm:AdministrativeUnit ;
                gadm:hasName 'Linz' ;
                gadm:hasNationalLevel 3 ;
        } 
    }

    ?obs_ent a sosa:Observation ;
        sosa:hasSimpleResult ?obs_result ; 
        sosa:resultTime ?obs_time ;
        sosa:hasFeatureOfInterest ?foi_ent ;
        sosa:observedProperty ?obs_prop_ent .
    ?obs_prop_ent a sosa:ObservableProperty ;
        rdfs:label 'CO' .

    FILTER (YEAR(?obs_time) = 2020 && MONTH(?obs_time) = 1)
}
GROUP BY ?obs_time
ORDER BY ?obs_time