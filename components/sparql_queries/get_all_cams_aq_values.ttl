# What was the CO concentration in Linz for January 2020?

PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#> 
PREFIX gadm: <http://example.com/ontologies/gadm#>

SELECT ?obs_result ?obs_time ?foi_ent
WHERE {
    {
        SELECT ?foi_ent
        WHERE {
            ?foi_ent a sosa:FeatureOfInterest ;
                geo:intersects ?gadm_ent .
            ?gadm_ent a gadm:AdministrativeUnit ;
                gadm:hasName 'Linz' ;
                gadm:hasNationalLevel 3 ;
        } 
    }

    ?obs_ent a sosa:Observation ;
            sosa:hasSimpleResult ?obs_result ; 
            sosa:resultTime ?obs_time ;
            sosa:hasFeatureOfInterest ?foi_ent ;
            sosa:observedProperty ?obs_prop_ent .
    ?obs_prop_ent a sosa:ObservableProperty ;
        rdfs:label 'CO' .

    FILTER (YEAR(?obs_time) = 2020 && MONTH(?obs_time) = 1)
} 